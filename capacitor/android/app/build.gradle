apply plugin: 'com.android.application'

android {
	namespace "io.vmxy.vmapp"
	compileSdkVersion rootProject.ext.compileSdkVersion

	defaultConfig {
		applicationId "io.vmxy.appid"
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		versionCode 1
		versionName "1.0.0"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		aaptOptions {
			// Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
			// Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
			ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
		}
		ndk {
			abiFilters "armeabi","x86"
		}
	}
	applicationVariants.all { variant ->
		variant.outputs.all { output ->
			def outputFile = output.outputFile
			if (outputFile != null && outputFile.name.endsWith('.apk')) {
				// 显示app名称+当前版本号
				def fileName = "app-v${variant.versionName}"
				// 如果有渠道，显示渠道名""
				if (variant.flavorName != "office" && variant.flavorName!="release"){
					fileName = fileName + "-${variant.flavorName}"
				}
				// 加入打包时间
				//fileName = fileName + "-${releaseTime()}"
				// 如果是debug版本，加入beta版本号
				if ('debug'.equals(variant.buildType.name)) {
					fileName = fileName + "-debug.apk"
				} else {
					// 如果是release版本，直接显示
					fileName = fileName + ".apk"
				}
				outputFileName = fileName
			}
		}
	}
	signingConfigs {
		release {
			keyAlias 'key'
			keyPassword '123456'
			storeFile file('release.jks')
			storePassword 'Cc123456'
		}
	}
	flavorDimensions "full"
	productFlavors {
		x5 {
			applicationId "io.vmxy.appid.x5"
			dimension 'full'
			applicationIdSuffix ".x5"
			manifestPlaceholders = [app_name: "appName-x5", channel: "x5"]
		}
		office {
			applicationId "io.vmxy.appid"
			dimension 'full'
			manifestPlaceholders = [app_name: "appName", channel: "release"]
		}

	}
	buildTypes {
		debug {
			applicationIdSuffix ".debug"
			debuggable true
			minifyEnabled false
			versionNameSuffix ".debug"
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}
}

repositories {
	flatDir {
		dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
	implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
	implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
	implementation project(':capacitor-android')
	implementation files('src\\main\\libs\\tbs_sdk.jar')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation "junit:junit:$junitVersion"
	androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
	androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
	implementation project(':capacitor-cordova-android-plugins')
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
	implementation 'com.liulishuo.filedownloader:library:1.7.7'
	implementation 'com.squareup.okhttp3:okhttp:3.4.1'
}

apply from: 'capacitor.build.gradle'


try {
	def servicesJSON = file('google-services.json')
	if (servicesJSON.text) {
		apply plugin: 'com.google.gms.google-services'
	}
} catch (Exception e) {
	logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
